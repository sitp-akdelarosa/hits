<%
'
'SQLサーバへの接続設定
'dim Host,DBName,ID,PassW
'Host  ="192.168.16.14"		'DBサーバアドレス
'DbName="wavedb"			'DB名称
'ID    ="wave"			'DBログインID
'PassW ="wave"			'DBログインパスワード

'
'SQLサーバへの接続
'Input :DB接続Object、レコードセットObject
'
'Function ConnDBH(ObjConn, ObjRS)
' Set ObjConn = Server.CreateObject("ADODB.Connection")
'  ObjConn.Open "Provider=SQLOLEDB;Data Source=" & Host & ";" &_
'               "Database=" & DbName & ";User ID=" & ID & ";Password=" & PassW & ";"
'  Set ObjRS = Server.CreateObject("ADODB.Recordset")
'  ObjConn.BeginTrans
'End Function

Function ConnDBH(ObjConn,ObjRS)
  Set ObjConn = Server.CreateObject("ADODB.Connection")
  ObjConn.Open "wavedb","wave","wave"

  ObjConn.CursorLocation = 3	'''3:adUseClient	'''Added 20040227

  Set ObjRS = Server.CreateObject("ADODB.Recordset")
  ObjConn.BeginTrans
End Function

'
'SQLサーバへの切断
'Input :DB接続Object、レコードセットObject
'
Function DisConnDBH(ObjConn,ObjRS)
  Set ObjRS = Nothing
  ObjConn.CommitTrans
  ObjConn.Close
  Set ObjConn = Nothing
End Function
'
'ユーザーＩＤが有効かどうかのチェック
'Input :なし
'
Function CheckLoginH()
    ' セッションの有効性をチェック
    Dim strUserID
    strUserID = Trim(Session.Contents("userid"))

    ' セッションが無効なとき
    If strUserID="" Then
      dim strtmp
      strtmp = "ログインしてから"&Session.Timeout&"分経過しました。"
      jampErrerP "0","b000","00","事前情報入力","000",strtmp
    End If
End Function
'
'アクセスログ出力
'Input :画面ID、画面名、作業ID、確認用オプションデータ
'
Function WriteLogH(iNum, strScrName, iWrkNum, strOption)
  'サーバ日時の取得
    dim DayTime,day
    getDayTime DayTime
  'ユーザタイプコード
    dim UtypeA
    UtypeA = Array("","荷主","海貨","船社","港運","陸運")
    dim strDate,logPath,ObjFSO,ObjTS

    strDate = DayTime(0) & DayTime(1) & DayTime(2)
'    logPath = Server.MapPath("../log/" & strDate & ".log")
    logPath = Server.MapPath("/log/" & strDate & ".log")

    Set ObjFSO = Server.CreateObject("Scripting.FileSystemObject")
    Set ObjTS = ObjFSO.OpenTextFile(logPath,8,True)
    ObjTS.Write DayTime(0)&"/"&DayTime(1)&"/"&DayTime(2)&" "&DayTime(3)&":"&DayTime(4)&":"&Right("0" & Second(Now), 2)
    ObjTS.Write "," & iNum
    ObjTS.Write "," & strScrName
    ObjTS.Write "," & iWrkNum
    ObjTS.Write "," & UtypeA(Session.Contents("UType")) & "," & Session.Contents("userid")
    ObjTS.Write "," & Request.ServerVariables("SCRIPT_NAME")
    ObjTS.Write "," & Request.ServerVariables("REMOTE_ADDR")
    If strOption<>"" Then
        ObjTS.WriteLine "," & strOption
    Else
        ObjTS.WriteLine 
    End If
    ObjTS.Close
    Set ObjTS = Nothing
    Set ObjFSO = Nothing
End Function
'
'エラー画面への遷移
'Input :エラー画面フラグ、画面ID、作業ID、作業名、エラーコード、オプション
'
Function jampErrerP(WinFlag,dispId,wkID,wkName,errerCd,etc)
  on error goto 0		'エラートラップ解除
  Session.Contents("WinFlag")=WinFlag
  Session.Contents("dispId")=dispId
  Session.Contents("wkID")=wkID
  Session.Contents("wkName")=wkName
  Session.Contents("errerCd")=errerCd
  Session.Contents("etc")=etc
  Response.Redirect "./errer.asp"
End Function
'
'エラー画面への遷移
'Input :DB接続Object、レコードセットObject、エラー画面フラグ、画面ID、作業ID、作業名、エラーコード、オプション
'
Function jampErrerPDB(ObjConn,WinFlag,dispId,wkID,wkName,errerCd,etc)
'ロールバックとDB切断
  ObjConn.RollbackTrans
  ObjConn.Close
  Set ObjConn = Nothing
  on error goto 0		'エラートラップ解除
  Session.Contents("WinFlag")=WinFlag
  Session.Contents("dispId")=dispId
  Session.Contents("wkID")=wkID
  Session.Contents("wkName")=wkName
  Session.Contents("errerCd")=errerCd
  Session.Contents("etc")=etc
  Response.Redirect "./errer.asp"
End Function

'
'サーバ日時の取得
'Input :Array(5)=(YYYY,MM,DD,hh,mm)
'
Function getDayTime(DayTime)
  dim MyDate
  MyDate = Now
  DayTime = Array(Year(MyDate),Right("0"&Month(MyDate),2),Right(Date(),2),Right("0"&Hour(MyDate),2),Right("0"&Minute(MyDate),2))
End Function
'
'輸入コンテナテーブルのコンテナ存在チェック
'Input :DB接続Object、レコードセットObject、コンテナ／BL番号、作業フラグ、戻り値
'Output:ret(存在する=>true　存在しない=>false) 
Function checkImportCont(ObjConn, ObjRS,strNum, Flag, ret)
  dim StrSQL
  If Flag <> 4 Then
    StrSQL = "SELECT count(ContNo) AS num FROM ImportCont WHERE ContNo=" & strNum
  Else
    StrSQL = "SELECT count(ContNo) AS num FROM ImportCont WHERE BLNo=" & strNum
  End If
  ObjRS.Open StrSQL, ObjConn
  if err <> 0 then
    DisConnDBH ObjConn, ObjRS	'DB切断
    jampErrerP "1","b000","00","輸入コンテナテーブル","101","SQL：<BR>"&StrSQL
  end if
  If ObjRS("num") <> 0 Then
    ret = true
  Else
    ret = false
  End If
  ObjRS.close
End Function
'
'CW-014 add strart
'
'輸入コンテナテーブルのコンテナ完了チェック
'Input :DB接続Object、レコードセットObject、コンテナ／BL番号、作業フラグ、戻り値
'Output:ret(未完了=>true　完了=>false) 
Function checkImportContComp(ObjConn, ObjRS,strNum, Flag, ret)
  dim StrSQL
  If Flag <> 4 Then
    StrSQL = "SELECT count(ContNo) AS num FROM ImportCont WHERE ContNo=" & strNum 
  Else
    StrSQL = "SELECT count(ContNo) AS num FROM ImportCont WHERE BLNo=" & strNum 
  End If
  StrSQL = StrSQL & " AND CYDelTime Is Null "
  ObjRS.Open StrSQL, ObjConn
  if err <> 0 then
    DisConnDBH ObjConn, ObjRS	'DB切断
    jampErrerP "1","b000","00","輸入コンテナテーブル完了チェック","101","SQL：<BR>"&StrSQL
  end if
  If ObjRS("num") <> 0 Then
    ret = true
  Else
    ret = false
  End If
  ObjRS.close
End Function
'CW-014 add end
'
'IT共通テーブルの重複チェック
'Input :DB接続Object、レコードセットObject、コンテナ／BL番号、処理区分、作業フラグ、作業番号、戻り値
'Output:ret(重複なし=>true　重複有り=>false) 
'      :WkNo(作業番号)
Function checkComInfo(ObjConn, ObjRS, strNum, WFlag, Flag, WkNo, ret)
  dim StrSQL,Tmp1,Tmp2
  ret = true
  If Flag <> 4 Then
   If WFlag = 1 Then
'     StrSQL="SELECT Count(WkContrlNo) AS num from hITFullOutSelect "&_
'            "WHERE ContNo="& strNum
      StrSQL="SELECT Count(A.WkContrlNo) AS num from hITCommonInfo A LEFT JOIN hITFullOutSelect B "&_
             "ON A.WkContrlNo=B.WkContrlNo WHERE B.ContNo=" & strNum &" AND A.Process='R' "&_
             "AND A.WkType="& WFlag &"AND A.WorkCompleteDate Is Null"
'20030911 ADD This Item "AND A.WorkCompleteDate Is Null" 
     ObjRS.Open StrSQL, ObjConn
     if err <> 0 then
       DisConnDBH ObjConn, ObjRS	'DB切断
       jampErrerP "1","b000","00","IT共通テーブル","101","SQL：<BR>"&StrSQL
     end if
     Tmp1=ObjRS("num")
     ObjRS.close
   Else
     Tmp1=0
   End If
   If Tmp1 <> 0 Then
      StrSQL="SELECT WkNo from hITCommonInfo A LEFT JOIN hITFullOutSelect B "&_
             "ON A.WkContrlNo=B.WkContrlNo WHERE B.ContNo=" & strNum &" AND A.Process='R' "&_
             "AND A.WkType="& WFlag &"AND A.WorkCompleteDate Is Null"
'20030911 ADD This Item "AND A.WorkCompleteDate Is Null" 
      ObjRS.Open StrSQL, ObjConn
      if err <> 0 then
        DisConnDBH ObjConn, ObjRS	'DB切断
        jampErrerP "1","b000","00","IT共通テーブル","102","SQL：<BR>"&StrSQL
      end if
      If Not IsNull(ObjRS("WkNo")) Then
        WkNo=ObjRS("WkNo")
        ret =false
      End If
      ObjRS.close
    Else
      StrSQL="SELECT Count(WkContrlNo) AS num from hITCommonInfo "&_
             "WHERE ContNo="& strNum &" AND Process='R' AND WkType="& WFlag &" AND WorkCompleteDate Is Null"
'20030911 ADD This Item "AND WorkCompleteDate Is Null" 
      ObjRS.Open StrSQL, ObjConn
      if err <> 0 then
        DisConnDBH ObjConn, ObjRS	'DB切断
        jampErrerP "1","b000","00","IT共通テーブル","101","SQL：<BR>"&StrSQL
      end if
      Tmp2=ObjRS("num")
      ObjRS.close
      If Tmp2 <> 0 Then
        StrSQL="SELECT WkNo from hITCommonInfo WHERE ContNo="& strNum &" AND Process='R' "&_
                "AND WkType="& WFlag&"AND WorkCompleteDate Is Null"
'20030911 ADD This Item "AND WorkCompleteDate Is Null" 
        ObjRS.Open StrSQL, ObjConn
        if err <> 0 then
          DisConnDBH ObjConn, ObjRS	'DB切断
          jampErrerP "1","b000","00","IT共通テーブル","102","SQL：<BR>"&StrSQL
        end if
        WkNo=ObjRS("WkNo")
        ObjRS.close
        ret=false
      End If
    End If
  Else
    StrSQL="SELECT Count(WkContrlNo) AS num from hITCommonInfo WHERE BLNo="& strNum &" AND Process='R' "&_
           "AND WorkCompleteDate Is Null"
'20030911 ADD This Item "AND WorkCompleteDate Is Null" 
    ObjRS.Open StrSQL, ObjConn
    if err <> 0 then
      jampErrerP "1","b000","00","IT共通テーブル","101","SQL：<BR>"&StrSQL
    end if
    Tmp1=ObjRS("num")
    ObjRS.close
    If Tmp1 <> 0 Then
      StrSQL="SELECT WkNo from hITCommonInfo WHERE BLNo=" & strNum &" AND Process='R' "&_
             "AND WorkCompleteDate Is Null"
'20030911 ADD This Item "AND WorkCompleteDate Is Null" 
      ObjRS.Open StrSQL, ObjConn
      if err <> 0 then
        DisConnDBH ObjConn, ObjRS	'DB切断
          jampErrerP "1","b000","00","IT共通テーブル","102","SQL：<BR>"&StrSQL
      end if
      WkNo=ObjRS("WkNo")
      ObjRS.close
      ret=false
    End If
  End If
End Function
'
'個別ブッキングテーブルの重複チェック&ブッキング指示テーブルの重複チェック
'Input :DB接続Object、レコードセットObject、ブックNo、登録者コード、指示先コード
'      :登録済み指示先CSV、処理区分1、処理区分2、戻り値
'Output:ret(重複なし又は処理区分「D」=>true　重複有り=>false) 
'      :PFlag(処理区分)
Function checkSPBook(ObjConn,ObjRS,BookNo,USER,Code,strCodes,PFlag1,PFlag2,ret)
  StrSQL = "SELECT Count(BookNo) AS Num FROM SPBookInfo WHERE BookNo='"& BookNo &"' "
  ObjRS.Open StrSQL, ObjConn
  if err <> 0 then
    DisConnDBH ObjConn, ObjRS	'DB切断
      jampErrerP "1","b000","00","個別ブッキングテーブル","101","SQL：<BR>"&StrSQL
  end if
  If ObjRS("Num") <> 0 Then
    ObjRS.close
    StrSQL = "SELECT Process FROM SPBookInfo WHERE BookNo='"& BookNo &"' "
    ObjRS.Open StrSQL, ObjConn
    if err <> 0 then
      DisConnDBH ObjConn, ObjRS	'DB切断
        jampErrerP "1","b000","00","個別ブッキングテーブル","102","SQL：<BR>"&StrSQL
    end if
    PFlag1=ObjRS("Process")
    If PFlag1="R" Then
      ObjRS.close
      StrSQL = "SELECT Count(BookNo) AS Num FROM BookingAssign "&_
               "WHERE BookNo='"& BookNo &"' AND SenderCode='"& USER &"' "&_
               "AND Process='R'"
      ObjRS.Open StrSQL, ObjConn
      if err <> 0 then
        DisConnDBH ObjConn, ObjRS	'DB切断
        jampErrerP "1","b000","00","ブッキング指示テーブル","101","SQL：<BR>"&StrSQL
      end if
      If ObjRS("Num") > 0 Then
        If ObjRS("Num")=1 Then
          PFlag1="1"
        Else
          PFlag1="3"
        End If
      Else
        PFlag1="2"
      End If
'      ret = false
    Else
      PFlag1="2"
'      ret = true
    End If
  Else
    PFlag1="0"
  End If
  ObjRS.close
'3th ADD ↓↓↓↓↓↓↓↓↓↓↓↓
  If Trim(Code)="" OR Code=Null Then
    Code=" "
  End If
  StrSQL = "SELECT Count(BookNo) AS Num FROM BookingAssign "&_
           "WHERE BookNo='"& BookNo &"' AND SenderCode='"& USER &"' "&_
           "AND TruckerCode='" & Code & "'"
  ObjRS.Open StrSQL, ObjConn
  if err <> 0 then
    DisConnDBH ObjConn, ObjRS	'DB切断
      jampErrerP "1","b000","00","ブッキング指示テーブル","101","SQL：<BR>"&StrSQL
  end if
  If ObjRS("Num") <> 0 Then
    ObjRS.close
    StrSQL = "SELECT TruckerCode,Process FROM BookingAssign "&_
             "WHERE BookNo='"& BookNo &"' AND SenderCode='"& USER &"'"
    ObjRS.Open StrSQL, ObjConn
    if err <> 0 then
      DisConnDBH ObjConn, ObjRS	'DB切断
        jampErrerP "1","b000","00","ブッキング指示テーブル","101","SQL：<BR>"&StrSQL
    end if
    strCodes=""
    Do Until ObjRS.EOF
      If Trim(ObjRS("Process"))="R" Then
        If Trim(Code) = Trim(ObjRS("TruckerCode")) Then
          ret = false
        End If
        If Trim(ObjRS("TruckerCode"))="" Then
          strCodes=strCodes &"なし,"
        Else
          strCodes=strCodes & Trim(ObjRS("TruckerCode")) &","
        End If
      End If
      ObjRS.MoveNext
    Loop
    PFlag2="1"
  Else
    PFlag2="0"
  End If
  ObjRS.close
End Function
'
'作業管理番号取得
'Input :DB接続Object、レコードセットObject、ユーザ略称、作業管理番号
'OutPut:作業管理番号
Function getWkContrlNo(ObjConn,ObjRS,sUN,WkContrlNo)
    dim StrSQL
    dim tmpContrlNo		'ADD 20040107
'Change 20050909 for 同時登録回避 By N.Ooshige
'    StrSQL = "SELECT CurrentVal FROM mAutoNumber WHERE TypeCode='12'"
    StrSQL = "BEGIN TRAN getWkContrlNo"&sUN &" "
    ObjConn.Execute(StrSQL)
    StrSQL = "SELECT CurrentVal FROM mAutoNumber WITH(XLOCK) WHERE TypeCode='12'"
'Change 20050909 END
    ObjRS.Open StrSQL, ObjConn
    if err <> 0 then
      DisConnDBH ObjConn, ObjRS	'DB切断
        jampErrerP "1","b000","00","作業管理番号テーブル","102","SQL：<BR>"&StrSQL
    end if
    WkContrlNo = ObjRS("CurrentVal")+1
    tmpContrlNo = WkContrlNo	'ADD 20040107
    ObjRS.close
    If Month(Now)*100000000 > WkContrlNo Then
      WkContrlNo = Month(Now)*100000000 +1
'20040107 Change    ElseIf Month(Now) = 1 Then
    ElseIf Month(Now) = 1 AND WkContrlNo/100000000>=12 Then
      WkContrlNo = 100000001
    End If
    dim ret
    ret= false
'ADD 作業管理番号重複時の処理 2003/08/04 ↓↓↓↓↓↓↓↓↓↓↓
    Do Until ret
     StrSQL = "SELECT COUNT(ITC.WkContrlNo) AS Expr1,COUNT(ITF.WkContrlNo) AS Expr2,COUNT(ITR.WkContrlNo) AS Expr3 "&_
               "FROM hITReference ITR "&_
               "FULL OUTER JOIN hITFullOutSelect ITF ON ITR.WkContrlNo = ITF.WkContrlNo "&_
               "FULL OUTER JOIN hITCommonInfo ITC ON ITF.WkContrlNo = ITC.WkContrlNo "&_
               "WHERE (ITC.WkContrlNo="& WkContrlNo &") OR (ITF.WkContrlNo="& WkContrlNo &") "&_
               "OR (ITR.WkContrlNo ="& WkContrlNo &")"
      ObjRS.Open StrSQL, ObjConn
      if err <> 0 then
        DisConnDBH ObjConn, ObjRS	'DB切断
          jampErrerP "1","b000","00","作業管理番号テーブル","102","SQL：<BR>"&StrSQL
      end if
      If ObjRS("Expr1")+ObjRS("Expr2")+ObjRS("Expr3") = 0 Then
        ret = true
      Else
'20040107 Change        WkContrlNo = WkContrlNo +1
        WkContrlNo = tmpContrlNo +1000
      End If
      ObjRS.close
    Loop
'ADD 作業管理番号重複時の処理 2003/08/04 ↑↑↑↑↑↑↑↑↑↑↑
    StrSQL = "UPDATE mAutoNumber SET CurrentVal = "& WkContrlNo &", UpdtTime='"& now() &"'," &_
             "UpdtPgCd='PREDEF01',UpdtTmnl='"& sUN &"' WHERE TypeCode='12'"
    ObjConn.Execute(StrSQL)
    if err <> 0 then
      Set ObjRS = Nothing
      jampErrerPDB ObjConn, "1","b000","00","作業管理番号テーブル","104","SQL：<BR>"&StrSQL
    end if
'ADD 20050909 for 同時登録回避 By N.Ooshige
    StrSQL = "COMMIT TRAN getWkContrlNo"&sUN &" "
    ObjConn.Execute(StrSQL)
'ADD 20050909 END
End Function

'ヘッド会社コード存在チェック
'Input :DB接続Object、レコードセットObject、ヘッド会社コード配列、戻り値
'OutPut:ret(存在する=>true　存在しない=>false)
Function checkHdCd(ObjConn,ObjRS,CMPcd,ret)
  dim StrSQL,check,i
  check=0
  For i=1 to 4
    If CMPcd(i) <> "" And CMPcd(i) <> "**" Then
      StrSQL="SELECT count(*) AS num FROM mUsers Where HeadCompanyCode='"& CMPcd(i) &"'"
      ObjRS.Open StrSQL, ObjConn
      if err <> 0 then
        DisConnDBH ObjConn, ObjRS	'DB切断
          jampErrerP "1","b000","00","IT業者共通マスタテーブル","101","SQL：<BR>"&StrSQL
      end if
      If ObjRS("num") = 0 Then
        check = check+1
      End If
      ObjRS.close
    End If
  Next
  If check=0 Then
    ret=true
  Else
    ret=false
  End If
End Function

'
'INIファイルからパラメータを読込む
'Input :Array(1)
'OutPut:Array(0)=ALARM、Array(1)=COMPLETED_DATA
Function getIni(param)
  dim ObjFSO,ObjTS,tmpStr
  Set ObjFSO = Server.CreateObject("Scripting.FileSystemObject")
  '--- ファイルを開く（読み取り専用） ---
  Set ObjTS = ObjFSO.OpenTextFile(Server.Mappath("./INI/PREDEF.INI"),1,false)
  '--- ファイルデータの読込み ---
  Do Until ObjTS.AtEndofStream
    tmpStr = Split(ObjTS.ReadLine, "=", 3, 1)
    Select Case tmpStr(0)
      Case "ALARM"
        param(0) = tmpStr(1)
      Case "COMPLETED_DATA"
        param(1) = tmpStr(1)
      Case "INPUT_DATA"
        param(2) = tmpStr(1)
    End Select
  Loop
  ObjTS.Close
  Set ObjTS = Nothing
  Set ObjFSO = Nothing
End Function

'ヘッド会社コード表示制御
'Input :ヘッド会社コード配列、アップフラグ
'
Function chengeCompCd(CMPcd,UpFlag)
  dim hCd,check,User,i
  hCd    = Session.Contents("COMPcd")
  User   = Session.Contents("userid")
  check=Array(0,0,0,0,0,0)
    If CMPcd(0) = UCase(User) Then
      UpFlag = 1
      check(0)=1
      check(1)=1
    End If
    For i=1 to 4
      If CMPcd(i) = hCd Then
        UpFlag = i+1
        check(i-1)=1
        check(i)=1
        check(i+1)=1
      End If
    Next
    For i=1 to 4
      If check(i) = 0 Then
        CMPcd(i)="**"
      End If
    Next
End Function
'ヘッド会社コード入力制御
'Input :ヘッド会社コード配列、アップフラグ
'
Function saveCompCd(CMPcd,UpFlag)
  dim hCd,User,i
  hCd    = Session.Contents("COMPcd")
  User   = UCase(Session.Contents("userid"))
  Select Case UpFlag
    Case "5"
'CW-0037     If CMPcd(4) = hCd Then
      CMPcd(1) = "'"& CMPcd(1) &"' readOnly"
      CMPcd(2) = "'"& CMPcd(2) &"' readOnly"
      CMPcd(3) = "'"& CMPcd(3) &"' readOnly"
      CMPcd(4) = "'"& CMPcd(4) &"' readOnly"
    Case "4"
'CW-0037    ElseIf CMPcd(3) = hCd Then
      CMPcd(1) = "'"& CMPcd(1) &"' readOnly"
      CMPcd(2) = "'"& CMPcd(2) &"' readOnly"
      CMPcd(3) = "'"& CMPcd(3) &"' readOnly"
'CW-0017      CMPcd(4) = "'"& CMPcd(4) &"'"
      CMPcd(4) = "'"& CMPcd(4) &"' onBlur='checkIDF(0)'"
    Case "3"
'CW-0037    ElseIf CMPcd(2) = hCd Then
      CMPcd(1) = "'"& CMPcd(1) &"' readOnly"
      CMPcd(2) = "'"& CMPcd(2) &"' readOnly"
'CW-0017      CMPcd(3) = "'"& CMPcd(3) &"'"
      CMPcd(3) = "'"& CMPcd(3) &"' onBlur='checkIDF(0)'"
      CMPcd(4) = "'"& CMPcd(4) &"' readOnly"
    Case "2"
'CW-0037    ElseIf CMPcd(1) = hCd Then
      CMPcd(1) = "'"& CMPcd(1) &"' readOnly"
'CW-0017      CMPcd(2) = "'"& CMPcd(2) &"'"
      CMPcd(2) = "'"& CMPcd(2) &"' onBlur='checkIDF(0)'"
      CMPcd(3) = "'"& CMPcd(3) &"' readOnly"
      CMPcd(4) = "'"& CMPcd(4) &"' readOnly"
    Case "1"
'CW-0037    Else
'CW-0017      CMPcd(1) = "'"& CMPcd(1) &"'"
      CMPcd(1) = "'"& CMPcd(1) &"' onBlur='checkIDF(0)'"
      CMPcd(2) = "'"& CMPcd(2) &"' readOnly"
      CMPcd(3) = "'"& CMPcd(3) &"' readOnly"
      CMPcd(4) = "'"& CMPcd(4) &"' readOnly"
    End Select
'CW-0037    End If
End Function

'データ受渡しメソッド		'CW-055 ADD
'Input :データ数、データテーブル
'
Function Set_Data(Num,DtTbl)
  dim i,j
  Response.Write "  <INPUT type=hidden name='DataNum' value='" & Num & "'>" & vbCrLf
  For i = 0 to Num
    Response.Write "  <INPUT type=hidden name='Datatbl" & i & "' value='"
    For j=0 To UBound(DtTbl(i))-1
      Response.Write DtTbl(i)(j) & ","
    Next
    Response.Write DtTbl(i)(j) & "'>" & vbCrLf
  Next
End Function

'データ受取りメソッド		'CW-055 ADD
'Input :データ数、データテーブル
'
Function Get_Data(Num,DtTbl)
  dim param
  Num=Request("DataNum")
  ReDim DtTbl(Num)
  For Each param In Request.Form
    If Left(param, 7) = "Datatbl" Then
      DtTbl(Mid(param,8))=Split(Request.Form(param), ",", -1, 1)
    End If
  Next
End Function

'作業番号採番		'3th ADD
'Input :DB接続Object、レコードセットObject、ユーザID、作業番号
'OutPut:作業番号
Function getWkNo(ObjConn,ObjRS,USER,WkContrlNo)
  dim StrSQL
  StrSQL = "SELECT Count(WkNo) AS Num FROM hITWkNo where Status='3'"
  ObjRS.Open StrSQL, ObjConn
  if err <> 0 then
    ObjRS.Close
    Set ObjRS = Nothing
    jampErrerPDB ObjConn, "1","b001","00","作業番号管理テーブル","101","SQL：<BR>"&StrSQL
  end if
'ADD 20050909 for 同時登録回避 By N.Ooshige
  StrSQL = "BEGIN TRAN getWkNo"&WkContrlNo &" "
  ObjConn.Execute(StrSQL)
'ADD 20050909 END
  If ObjRS("Num") <> 0 Then
    ObjRS.close
'Change 20050909 for 同時登録回避 By N.Ooshige
'    StrSQL = "SELECT WkNo FROM hITWkNo where Status='3'"
    StrSQL = "SELECT WkNo FROM hITWkNo WITH(XLOCK) where Status='3'"
'Change 20050909 End
    ObjRS.Open StrSQL, ObjConn
    if err <> 0 then
      ObjRS.Close
      Set ObjRS = Nothing
      jampErrerPDB ObjConn, "1","b001","00","作業番号管理テーブル","102","SQL：<BR>"&StrSQL
    end if
    SakuNo=ObjRS("WkNo")
    ObjRS.close
    StrSQL = "UPDATE hITWkNo SET Status='2' WHERE WkNo ='"& SakuNo &"'"
    ObjConn.Execute(StrSQL)
    if err <> 0 then
      Set ObjRS = Nothing
      jampErrerPDB ObjConn, "1","b001","00","作業番号管理テーブル","104","SQL：<BR>"&StrSQL
    end if
  Else
    ObjRS.close
'Change 20050909 for 同時登録回避 By N.Ooshige
'    StrSQL = "SELECT CurrentVal FROM mAutoNumber WHERE TypeCode='11'"
    StrSQL = "SELECT CurrentVal FROM mAutoNumber WITH(XLOCK) WHERE TypeCode='11'"
'Change 20050909 End
    ObjRS.Open StrSQL, ObjConn
    if err <> 0 then
      ObjRS.Close
      Set ObjRS = Nothing
      jampErrerPDB ObjConn, "1","b001","00","番号管理マスタ","102","SQL：<BR>"&StrSQL
    end if
    SakuNo = ObjRS("CurrentVal")+1
    ObjRS.close
    StrSQL = "UPDATE mAutoNumber SET CurrentVal = "& SakuNo &", UpdtTime='"& now() &"',"&_
             "UpdtPgCd='PREDEF01',UpdtTmnl='"& USER &"' WHERE TypeCode='11'"
    ObjConn.Execute(StrSQL)
    if err <> 0 then
      Set ObjRS = Nothing
      jampErrerPDB ObjConn, "1","b001","00","作業番号管理テーブル","104","SQL：<BR>"&StrSQL
    end if
    SakuNo = Right("0000" & Hex(SakuNo),5)
    StrSQL = "Insert Into hITWkNo (WkNo,UpdtTime,UpdtPgCd,UpdtTmnl,Status) values ('" &_
             SakuNo &"','"& Now() &"','PREDEF01','"& USER &"','2')"
    ObjConn.Execute(StrSQL)
    if err <> 0 then
      Set ObjRS = Nothing
      jampErrerPDB ObjConn, "1","b001","00","作業番号管理テーブル","103","SQL：<BR>"&StrSQL
    end if
  End If
'ADD 20050909 for 同時登録回避 By N.Ooshige
  StrSQL = "COMMIT TRAN getWkNo"&WkContrlNo &" "
  ObjConn.Execute(StrSQL)
'ADD 20050909 END
End Function
'
'ブッキング指示テーブルの重複チェック		'3th ADD
'Input :DB接続Object、レコードセットObject、ブックNo、登録者コード
'      :戻り値
'Output:ret(重複なし又は処理区分「D」=>true　重複有り=>false) 
Function BookAs(ObjConn,ObjRS,BookNo,USER,ret)
  StrSQL = "SELECT Count(BookNo) AS Num FROM BookingAssign "&_
           "WHERE BookNo='"& BookNo &"' AND Process='R'"
  ObjRS.Open StrSQL, ObjConn
  if err <> 0 then
    DisConnDBH ObjConn, ObjRS	'DB切断
      jampErrerP "1","b000","00","ブッキング指示テーブル","101","SQL：<BR>"&StrSQL
  end if
  If ObjRS("Num")>0 Then
    ObjRS.close
    StrSQL = "SELECT SenderCode,Process FROM BookingAssign "&_
             "WHERE BookNo='"& BookNo &"' AND Process='R'"
    ObjRS.Open StrSQL, ObjConn
    if err <> 0 then
      DisConnDBH ObjConn, ObjRS	'DB切断
        jampErrerP "1","b000","00","ブッキング指示テーブル","102","SQL：<BR>"&StrSQL
    end if
    If Trim(ObjRS("SenderCode")) <> USER Then
      ret=false
    End If
  End If
  ObjRS.close
End Function
%>