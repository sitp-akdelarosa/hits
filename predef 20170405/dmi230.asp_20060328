<% @LANGUAGE = VBScript %>
<%
'_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
'_/	SystemName	:Hits					_/
'_/	FileName	:dmi230.asp				_/
'_/	Function	:事前空搬出入力確認画面			_/
'_/	Date		:2003/05/28				_/
'_/	Code By		:SEIKO Electric.Co 大重			_/
'_/	Modify		:C-002	2003/08/06	備考欄追加	_/
'_/	Modify		:3th	2003/01/31	3次全面改修	_/
'_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
%>
<% Option Explicit %>
<%
	'HTTPコンテンツタイプ設定
	Response.ContentType = "text/html; charset=Shift_JIS"
%>
<!--#include File="Common.inc"-->
<%
'セッションの有効性をチェック
  CheckLoginH

'データ所得
  dim BookNo, COMPcd0, COMPcd1,Mord, ret, ErrerM,i
  BookNo = Trim(Request("BookNo"))
  COMPcd0 = Request("COMPcd0")
  COMPcd1 = Request("COMPcd1")
  Mord    = Request("Mord")
  ret = true
  ErrerM = ""
'エラートラップ開始
  on error resume next
'DB接続
  dim ObjConn, ObjRS, StrSQL
  ConnDBH ObjConn, ObjRS

'ブックの重複登録チェック
  dim strCodes,dummy1, dummy2
  If Mord=0 OR (Mord=1 AND COMPcd1 <> Request("oldCOMPcd1")) Then
    checkSPBook ObjConn, ObjRS, BookNo,COMPcd0,COMPcd1,strCodes,dummy1, dummy2, ret
    If Not ret Then
      ErrerM="指定したブッキングNoは指示先「"& Left(strCodes,Len(strCodes)-1) &"」で既に登録されています。"
    End If
  End If
  if err <> 0 then
    DisConnDBH ObjConn, ObjRS	'DB切断
    jampErrerP "2","b303","01","ブッキング指示テーブル","101","SQL：<BR>"&StrSQL
  end if
  If (ret) Then
   'ヘッドIDのチェック
    dim CMPcd
    'CW-327 Change
    'CMPcd = Array("",COMPcd1,"","","")
    CMPcd = Array("",Trim(COMPcd1),"","","")
    checkHdCd ObjConn, ObjRS, CMPcd, ret
    If (ret) Then
    Else
      ErrerM="指定された会社コードは存在しません。<BR>「戻る」ボタンを押下し、再入力してください。"
    End If
  End If

'ブックの搬出完了チェック
  If ret Then
    dim cmpNum
    StrSQL = "SELECT Count(EXC.BookNo) AS numB, Count(Pic.Qty) AS numQ "&_
             "FROM ExportCont AS EXC INNER JOIN Pickup AS Pic ON (EXC.VslCode = Pic.VslCode) "&_
             "AND (EXC.VoyCtrl = Pic.VoyCtrl) AND (EXC.BookNo = Pic.BookNo) "&_
             "WHERE EXC.BookNo='"& BookNo &"' AND EmpDelTime IS NOT NULL"
    ObjRS.Open StrSQL, ObjConn
    if err <> 0 then
      DisConnDBH ObjConn, ObjRS
      jampErrerP "2","b303","01","空搬出：搬出完了チェック","101","SQL:<BR>"&strSQL
    end if
    cmpNum=ObjRS("numB")
    If ObjRS("numQ")<>0 Then
      ObjRS.close
      StrSQL = "SELECT Pic.Qty "&_
               "FROM ExportCont AS EXC INNER JOIN Pickup AS Pic ON (EXC.VslCode = Pic.VslCode) "&_
               "AND (EXC.VoyCtrl = Pic.VoyCtrl) AND (EXC.BookNo = Pic.BookNo) "&_
               "WHERE EXC.BookNo='"& BookNo &"' GROUP BY Pic.Qty"
      ObjRS.Open StrSQL, ObjConn
      if err <> 0 then
        DisConnDBH ObjConn, ObjRS
        jampErrerP "2","b303","01","空搬出：搬出完了チェック","101","SQL:<BR>"&strSQL
      end if
      If cmpNum = ObjRS("Qty") Then
        ErrerM="<DIV class=alert><注意>指定のブッキング番号は搬出が完了しています。</DIV>"
      End If
    End If
    ObjRS.close
  End If
'DB接続解除
  DisConnDBH ObjConn, ObjRS
'エラートラップ解除
  on error goto 0

  dim tmpstr
  If ret Then
    tmpstr=",入力内容の正誤:0(正しい)"
  Else
    tmpstr=",入力内容の正誤:1(誤り)"
  End If
  If Request("Mord")=0 Then
    WriteLogH "b302", "空搬出事前情報入力","02",BookNo&"/"&COMPcd1&tmpstr
  Else
    WriteLogH "b302", "空搬出事前情報入力","13",BookNo&"/"&COMPcd1&tmpstr
  End If
%>
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<HTML><HEAD>
<LINK REL="stylesheet" TYPE="text/css" HREF="./style.css">
<TITLE>空搬出情報入力確認</TITLE>
<META content="text/html; charset=Shift_JIS" http-equiv=Content-Type>
<SCRIPT src="./JS/Common.js"></SCRIPT>
<SCRIPT language=JavaScript>
<!--

function setParam(target){
//  window.resizeTo(500,260);
  bgset(target);
}

//登録
function GoEntry(printFlag){
  target=document.dmi230F;
  target.SijiF.value=printFlag
  target.action="./dmi240.asp";
  target.submit();
}
//戻る
function GoBackT(){
  target=document.dmi230F;
  target.action="./dmi220.asp";
  target.submit();
}
// -->
</SCRIPT>

<META content="MSHTML 5.00.2919.6307" name=GENERATOR></HEAD>
<BODY leftMargin=0 topMargin=0 marginheight="0" marginwidth="0" onLoad="setParam(document.dmi230F)">
<!-------------空搬出情報入力確認画面--------------------------->
<FORM name="dmi230F" method="POST">
<TABLE border=0 cellPadding=3 cellSpacing=0 width="100%">
  <TR>
    <TD colspan=2>
      <B>空搬出情報入力確認</B></TD></TR>
  <TR>
    <TD><DIV class=bgb>ブッキングＮｏ．</DIV></TD>
    <TD><INPUT type=text name="BookNoM" value="<%=Request("BookNoM")%>" readOnly size=40>
        <INPUT type=hidden name="BookNo" value="<%=Request("BookNo")%>"></TD></TR>
  <TR>
    <TD><DIV class=bgb>船社</DIV></TD>
    <TD><INPUT type=text name="shipFact" value="<%=Request("shipFact")%>" readOnly size=40></TD></TR>
  <TR>
    <TD><DIV class=bgb>船名</DIV></TD>
    <TD><INPUT type=text name="shipName" value="<%=Request("shipName")%>" readOnly size=40></TD></TR>
  <TR>
    <TD><DIV class=bgb>仕向地</DIV></TD>
    <TD><INPUT type=text name="delivTo" value="<%=Request("delivTo")%>" readOnly size=40></TD></TR>
  <TR>
    <TD><DIV class=bgb>会社コード(陸運)</DIV></TD>
    <TD><INPUT type=text name="COMPcd1" value="<%=COMPcd1%>" size=5  readOnly>
        <INPUT type=hidden name="oldCOMPcd1" value="<%=Request("oldCOMPcd1")%>"></TD></TR>
  <TR>
    <TD><DIV class=bgb>属性と本数</DIV></TD>
    <TD></TD></TR>
  <TR>
    <TD colspan=2>
    <TABLE border=0 cellPadding=0 cellSpacing=0 width=400 align=center>
      <TR><TD></TD><TD>サイズ</TD><TD>タイプ</TD><TD>高さ</TD><TD>材質</TD><TD>ピック場所</TD><TD></TD><TD>本数</TD></TR>
<% For i=0 To 4%>
      <TR><TD>(<%=i+1%>)</TD>
          <TD><INPUT type=text name="ContSize<%=i%>"   value="<%=Request("ContSize"&i)%>" size=4  readOnly></TD>
          <TD><INPUT type=text name="ContType<%=i%>"   value="<%=Request("ContType"&i)%>" size=4  readOnly></TD>
          <TD><INPUT type=text name="ContHeight<%=i%>" value="<%=Request("ContHeight"&i)%>" size=4  readOnly></TD>
          <TD><INPUT type=text name="Material<%=i%>"   value="<%=Request("Material"&i)%>"   size=4  readOnly></TD>
          <TD><INPUT type=text name="PickPlace<%=i%>"  value="<%=Request("PickPlace"&i)%>"  size=25 readOnly></TD>
          <TD>・・・</TD>
          <TD><INPUT type=text name="PickNum<%=i%>" value="<%=Request("PickNum"&i)%>" size=4  readOnly></TD></TR>
<% Next %>
    </TABLE>
    </TD></TR>
  <TR>
    <TD><DIV class=bgb>バン詰め日時</DIV></TD>
    <TD><INPUT type=text name="vanMon" value="<%=Request("vanMon")%>" size=3  readOnly>月
        <INPUT type=text name="vanDay" value="<%=Request("vanDay")%>" size=3  readOnly>日
        <INPUT type=text name="vanHou" value="<%=Request("vanHou")%>" size=3  readOnly>時
        <INPUT type=text name="vanMin" value="<%=Request("vanMin")%>" size=3  readOnly>分
        </TD></TR>
  <TR>
    <TD><DIV class=bgb>バン詰め場所１</DIV></TD>
    <TD><INPUT type=text name="vanPlace1" value="<%=Request("vanPlace1")%>" size=73  readOnly></TD></TR>
  <TR>
    <TD><DIV class=bgb>バン詰め場所２</DIV></TD>
    <TD><INPUT type=text name="vanPlace2" value="<%=Request("vanPlace2")%>" size=73  readOnly></TD></TR>
  <TR>
    <TD><DIV class=bgb>品名</DIV></TD>
    <TD><INPUT type=text name="goodsName" value="<%=Request("goodsName")%>" size=30  readOnly></TD></TR>
  <TR>
    <TD><DIV class=bgb>搬入先ＣＹ．ＣＹカット日</DIV></TD>
    <TD><INPUT type=text name="Terminal" value="<%=Request("Terminal")%>" readOnly>
        <INPUT type=text name="CYCut" value="<%=Request("CYCut")%>" readOnly></TD></TR>
  <TR>
    <TD><DIV class=bgb>備考１</DIV></TD>
    <TD><INPUT type=text name="Comment1" value="<%=Request("Comment1")%>" size=73  readOnly></TD></TR>
  <TR>
    <TD><DIV class=bgb>備考２</DIV></TD>
    <TD><INPUT type=text name="Comment2" value="<%=Request("Comment2")%>" size=73  readOnly></TD></TR>
  <TR>
    <TD colspan=2 align=center>
      <INPUT type=hidden name=Mord value="<%=Request("Mord")%>" >
      <INPUT type=hidden name=COMPcd0 value="<%=COMPcd0%>" >
      <INPUT type=hidden name=Res value="<%=Request("Res")%>" >
      <INPUT type=hidden name=SijiF value="" ><P><BR></P>
<% IF ret Then %>
       <P><%=ErrerM%></P>
       <INPUT type=button value="確定" onClick="GoEntry('No')">
<% Else %>
       <P><DIV class=alert><%=ErrerM%></DIV></P>
<% End If %>
       <INPUT type=button value="戻る" onClick="GoBackT()">
<% IF Mord=0  AND ret Then %>
       <P><INPUT type=button value="確定＆指示書印刷" onClick="GoEntry('Yes')"></P>
<% End If %>

    </TD></TR>

</TABLE>
</FORM>
<!-------------画面終わり--------------------------->
</BODY></HTML>
